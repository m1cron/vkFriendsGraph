buildscript {

    ext {
        servicePackage = 'ru.micron'
        servicePackageSlashes = servicePackage.replace('.', '/')

        ver = [
                springBoot              : '2.3.3.RELEASE',
                dependencyManagement    : '1.0.10.RELEASE',
                openapi                 : '5.0.0-beta2'
        ]
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:$ver.dependencyManagement",
                "org.springframework.boot:spring-boot-gradle-plugin:$ver.springBoot",
                "org.openapitools:openapi-generator-gradle-plugin:$ver.openapi"
    }
}

defaultTasks 'clean', 'build'

group = 'ru.micron'
rootProject.wrapper.gradleVersion = '7.1.1'

ext {
    defaultEncoding = 'UTF-8'

    ver = [
            openFeign               : '2.2.3.RELEASE',
            swagger2                : '2.9.2',
            swaggerAnnotations      : '2.1.1',
            lombok                  : '1.18.12',
            jacksonDataType         : '2.11.1',
            mapstruct               : '1.3.1.Final',
            logback                 : '1.2.3',
            logstashLogbackEncoder  : '6.2',
            apacheCommonsLang       : '3.11'
    ]
}

allprojects {
    repositories.addAll(rootProject.buildscript.repositories)
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR8'
        }
    }

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_15

    test {
        systemProperty 'spring.profiles.active', 'test'

        useJUnitPlatform()
    }

}